/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'james.baiera' at '9/25/15 11:18 AM' with Gradle 2.7
 */

// Apply the java plugin to add support for Java
apply {
    plugin 'java'
    plugin 'scala'
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile 'org.scala-lang:scala-library:2.11.1'
}

task exercise << {
    def chapterPropertyName = "chapter"
    if (!project.hasProperty(chapterPropertyName)) {
        throw new GradleException("You must specify the '$chapterPropertyName' property! (-P$chapterPropertyName=???)")
    }

    def chapter = project.getProperty(chapterPropertyName)
    def chapterPackage = "ch$chapter"
    def File scalaRoot = sourceSets.main.scala.srcDirs.first()
    def destDir = new File(new File(scalaRoot, "chapters"), chapterPackage)
    if(!destDir.exists() && !destDir.mkdir()) {
        throw new GradleException("Could not create '$destDir' directory.")
    }

    1.upto(10) {
        def exampleNumber = (it < 10) ? "0$it" : "$it"
        def exampleName = "Ex$exampleNumber"
        def file = new File(destDir, "${exampleName}.scala")
        if (!file.createNewFile()) {
            throw new GradleException("Could not create '${file.name}' file.")
        }
        file.write("""\
            |package chapters.$chapterPackage
            |
            |/**
            |  * Chapter $chapter, Example $exampleNumber:
            |  *
            |  */
            |object $exampleName extends App {
            |
            |}
        """.stripMargin())
    }
}